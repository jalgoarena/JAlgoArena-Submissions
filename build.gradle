buildscript {
    ext.fasterxml_version = '2.9.5'
    ext.xodus_version = '1.2.2'
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.40"
    id 'org.springframework.boot' version '1.5.10.RELEASE'
    id "jacoco"
}

apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'

apply from: "$rootDir/gradle/versioning.gradle"

group = 'jalgoarena'
description = """JAlgoArena Submissions"""

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

jar {
    baseName = 'jalgoarena-submissions'
}

dependencies {
    compile "com.fasterxml.jackson.core:jackson-databind:$fasterxml_version",
            "com.fasterxml.jackson.core:jackson-core:$fasterxml_version",
            "com.fasterxml.jackson.core:jackson-annotations:$fasterxml_version",
            "com.fasterxml.jackson.module:jackson-module-kotlin:$fasterxml_version",
            "org.springframework.kafka:spring-kafka",
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-actuator",
            'org.springframework.cloud:spring-cloud-starter-eureka',
            "org.jetbrains.kotlin:kotlin-stdlib",
            "org.jetbrains.xodus:xodus-openAPI:$xodus_version",
            "org.jetbrains.xodus:xodus-entity-store:$xodus_version"

    testCompile 'junit:junit:4.12',
            'pl.pragmatists:JUnitParams:1.0.5',
            "org.springframework.kafka:spring-kafka-test:2.1.4.RELEASE",
            "org.springframework.boot:spring-boot-starter-test",
            'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'
}

apply plugin: 'jacoco'

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

defaultTasks 'clean', 'bootRepackage'

task stage {
    dependsOn 'bootRepackage'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Edgware.SR3"
    }
}

task releaseZip(type: Zip, dependsOn: 'bootRepackage') {
    from('build/libs/') {
        include('*.jar')
    }
    from 'run.sh'
    archiveName "JAlgoArena-Submissions-${version}.zip"
    destinationDir(file('build/libs'))
}
